/*
 * This file is part of Material Audiobook Player.
 *
 * Material Audiobook Player is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or any later version.
 *
 * Material Audiobook Player is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Material Audiobook Player. If not, see <http://www.gnu.org/licenses/>.
 * /licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        targetSdkVersion 23
        versionCode 124
        versionName '2.7.2.1'
        vectorDrawables.useSupportLibrary = true
    }

    def minSdkDimension = "minSdk"
    flavorDimensions minSdkDimension
    productFlavors {
        regularMinSdk {
            dimension minSdkDimension
            minSdkVersion 15
        }
        largeMinSdk {
            dimension minSdkDimension
            minSdkVersion 21
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {

        String sharedTestDir = 'src/sharedTest/java'

        test {
            java.srcDirs += sharedTestDir
        }

        androidTest {
            java.srcDirs += sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        incremental false
    }
}

dependencies {
    // support library
    def supportVersion = "23.3.0"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:design:$supportVersion"

    // image loading
    compile "com.squareup.picasso:picasso:2.5.2"

    // material styled dialogs
    compile("com.afollestad.material-dialogs:core:0.8.5.5@aar") {
        transitive = true
    }

    // fab
    compile "com.getbase:floatingactionbutton:1.10.1"

    // crash reporting
    compile "ch.acra:acra:4.8.5"

    // dependency injection
    def daggerVersion = "2.0.2"
    compile "com.google.dagger:dagger:$daggerVersion"
    androidTestCompile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "javax.annotation:jsr250-api:1.0"

    //testing
    testCompile "junit:junit:4.12"
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude module: 'support-annotations'
    }
    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'org.easytesting:fest-assert-core:2.0M10'

    // rx extensions
    compile "io.reactivex:rxandroid:1.1.0"
    compile "io.reactivex:rxjava:1.1.3"
    compile 'com.jakewharton.rxbinding:rxbinding-kotlin:0.4.0'

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // player
    compile 'com.github.PaulWoitaschek:AndroidPlayer:0.009'

    // logs
    compile 'com.github.PaulWoitaschek:Slimber:1.0.4'
}
