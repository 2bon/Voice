/*
 * This file is part of Material Audiobook Player.
 *
 * Material Audiobook Player is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or any later version.
 *
 * Material Audiobook Player is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Material Audiobook Player. If not, see <http://www.gnu.org/licenses/>.
 * /licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: '../jacoco.gradle'


android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        targetSdkVersion 23
        versionCode 114
        versionName '2.6.2'
        vectorDrawables.useSupportLibrary = true
    }

    def minSdkDimension = "minSdk"
    def isSnapshotDimension = "snapshot"
    flavorDimensions minSdkDimension, isSnapshotDimension
    productFlavors {
        regularMinSdk {
            dimension minSdkDimension
            minSdkVersion 15
        }
        largeMinSdk {
            dimension minSdkDimension
            minSdkVersion 21
        }
        snapshot {
            dimension isSnapshotDimension
            versionName += "-SNAPSHOT"
        }
        noSnapshot {
            dimension isSnapshotDimension
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            resValue "string", "app_name", "Material Player"
        }
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "[DBG] Material Player"
            testCoverageEnabled = true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {

        String sharedTestDir = 'src/sharedTest/java'

        test {
            java.srcDirs += sharedTestDir
        }

        androidTest {
            java.srcDirs += sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        incremental false
    }
}

dependencies {
    // support library
    def supportVersion = "23.2.0"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:design:$supportVersion"

    // image loading
    compile "com.squareup.picasso:picasso:2.5.2"

    // material styled dialogs
    compile("com.afollestad.material-dialogs:core:0.8.5.5@aar") {
        transitive = true
    }

    // fab
    compile "com.getbase:floatingactionbutton:1.10.1"

    // crash reporting
    compile "ch.acra:acra:4.8.2"

    // dependency injection
    def daggerVersion = "2.0.2"
    compile "com.google.dagger:dagger:$daggerVersion"
    androidTestCompile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "javax.annotation:jsr250-api:1.0"

    //testing
    testCompile "junit:junit:4.12"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile "com.jayway.android.robotium:robotium-solo:5.5.3"
    testCompile "org.robolectric:robolectric:3.1-SNAPSHOT"
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    def okHttpVersion = "3.1.2"
    testCompile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    androidTestCompile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    testCompile "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"
    androidTestCompile "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"

    // rx extensions
    compile "io.reactivex:rxandroid:1.1.0"
    compile "io.reactivex:rxjava:1.1.1"
    compile 'com.jakewharton.rxbinding:rxbinding-kotlin:0.4.0'

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // player
    compile 'com.github.PaulWoitaschek:AndroidPlayer:0.008'

    // logs
    compile 'com.github.PaulWoitaschek:Slimber:1.0.4'
}
