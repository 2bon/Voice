apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "../artifacts.gradle"

android {

    // dynamically pass 21 to the compiler for build speedup without losing lint warnings.
    int minSdk = project.hasProperty('minSdk') ? minSdk.toInteger() : 16

    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "de.ph1b.audiobook"
        minSdkVersion minSdk
        targetSdkVersion 25
        versionCode 152
        versionName '2.9.3.5'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    testOptions.unitTests.returnDefaultValues = true

    lintOptions.abortOnError = false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    // support library
    def supportVersion = "25.1.1"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:transition:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    // image loading
    compile "com.squareup.picasso:picasso:2.5.2"

    // material styled dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.2.3'

    // fab
    compile "com.getbase:floatingactionbutton:1.10.1"

    // dependency injection
    def daggerVersion = "2.9"
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "javax.annotation:jsr250-api:1.0"

    //testing
    testCompile "junit:junit:4.12"
    testCompile "org.robolectric:robolectric:3.2.2"
    def vAssertJ = "1.7.1"
    testCompile "org.assertj:assertj-core:$vAssertJ"
    androidTestCompile "org.assertj:assertj-core:$vAssertJ"
    def vSupportTest = "0.5"
    def supportExclude = { exclude module: 'support-annotations' }
    androidTestCompile("com.android.support.test:runner:$vSupportTest", supportExclude)
    androidTestCompile("com.android.support.test:rules:$vSupportTest", supportExclude)
    androidTestCompile "org.mockito:mockito-core:2.4.3"

    // rx extensions
    compile "io.reactivex.rxjava2:rxjava:2.0.5"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
    compile 'com.github.akarnokd:rxjava2-interop:0.8.4'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // player
    compile 'com.google.android.exoplayer:exoplayer:r2.2.0'

    // logs
    compile 'com.github.PaulWoitaschek:Slimber:1.0.5'

    // permissions
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'

    def vPlayServices = "10.0.1"
    releaseCompile "com.google.firebase:firebase-crash:$vPlayServices"
    compile "com.google.firebase:firebase-core:$vPlayServices"

    // conductor
    def conductorVer = "2.1.0"
    compile "com.bluelinelabs:conductor:$conductorVer"
    compile "com.bluelinelabs:conductor-support:$conductorVer"
    compile "com.bluelinelabs:conductor-rxlifecycle2:$conductorVer"
}

apply plugin: 'com.google.gms.google-services'