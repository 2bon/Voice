apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersionsPlugin"
        classpath "io.fabric.tools:gradle:$versions.fabric"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                def candidate = selection.candidate
                if (candidate.group == 'org.assertj' && !candidate.version.startsWith("1")) selection.reject("Only version 1 of assertJ supported")
                if (['rc', 'beta', 'alpha'].any { forbidden -> candidate.version.contains(forbidden) }) selection.reject("blacklisted")
            }
        }
    }
}

task updateTranslations(type: Exec) {
    executable 'sh'
    args "-c", "tx pull -f --minimum-perc=5"
}