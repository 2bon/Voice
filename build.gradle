buildscript {
  apply from: 'dependencies.gradle'

  repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$versions.androidPlugin"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    classpath "io.fabric.tools:gradle:$versions.fabric"
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.9.1'
  id 'com.github.ben-manes.versions' version '0.15.0'
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

allprojects {
  repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
  }

  dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        def candidate = selection.candidate
        if (candidate.group == 'org.assertj' && !candidate.version.startsWith("1")) selection.reject("Only version 1 of assertJ supported")
        if (['rc', 'beta', 'alpha'].any { forbidden -> candidate.version.contains(forbidden) }) selection.reject("blacklisted")
        if (candidate.group == 'javax.annotation' && candidate.version == '1.0-20050927.133100') selection.reject()
      }
    }
  }
  configurations.all {
    resolutionStrategy {
      force libraries.support.v13
      force "com.squareup.okio:okio:1.13.0"
      force libraries.support.annotations
      force libraries.kotlinStdLib
    }
  }
}

apply from: "predexDisabler.gradle"

task updateTranslations(type: Exec) {
  executable 'sh'
  args "-c", "tx pull -af --minimum-perc=5"
}
