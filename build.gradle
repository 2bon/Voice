apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0-rc1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                def candidate = selection.candidate
                if (candidate.group == 'org.assertj' && !candidate.version.startsWith("1")) selection.reject("Only version 1 of assertJ supported")
                if (['beta', 'alpha'].any { forbidden -> candidate.version.contains(forbidden) }) selection.reject("blacklisted")
            }
        }
    }
}

task updateTranslations(type: Exec) {
    executable 'sh'
    args "-c", "tx pull -f --minimum-perc=5"
}